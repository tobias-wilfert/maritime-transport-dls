/// Water Modeling System
Model WMS {
    /// Base class for all Nodes so thay we can have a collection of them for the sequence
    abstract Node AnyNode{}

    /// Base class for all Nodes that can contain a Watercraft
    abstract Node Shippable {
        has_watercraft: boolean = false;
        got_this_step: boolean = false;
    }

    /// Base class for all Nodes that have one out-connection
    abstract Node InNode:  AnyNode {
        in0: OutNode;
        //------------
        // constraints:
        C_in0_not_self: $ self.in0 <> self $ /// In connection should not connect to self
        /// The in connection needs to match, that is:
        /// a - The out0 connection of the the node that is referenced by the in0 is this node
        /// b - The node that is referenced by in0 is a Confluence and its out1 is pointing to this node
        /// Note: These scenarios are exclusice and shouldn't be true at the same time
        C_outNode_connection_match: $ (self.in0.out0 == self) xor (self.in0.isKindOf(Confluence) and self.in0.out1 == self) $
    }

    /// Base class for all Nodes that have one in-connection
    abstract Node OutNode: AnyNode {
        out0: InNode;
        //------------
        // constraints:
        C_out0_not_self: $ self.out0 <> self $ /// The out connection should not connect to self
        /// The out connection needs to match, that is:
        /// a - The in0 connection of the the node that is referenced by the out0 is this node
        /// b - The node that is referenced by out0 is a Confluence and its in1 is pointing to this node
        /// Note: These scenarios are exclusice and shouldn't be true at the same time
        C_inNode_connection_match: $ (self.out0.in0 == self) xor (self.out0.isKindOf(Confluence) and self.out0.in1 == self) $
    }

    Node Sink[0..*]: InNode {
        /// Counter of how many Watercraft this has consumed so far.
        counter: int = 0;
        //------------
        // constraints:
        C_counter: $ self.counter >= 0 $ /// Counter needs to be positive
    }

    Node Source[0..*]: OutNode {
        /// Counter of how many Watercraft this has generated so far
        counter: int = 0;
        /// Number of steps that the need to be waited till another
        /// Watercraft is generated 
        rate: int;
        /// Counter for the Rate that goes to 1, if it hits zero a new
        /// Watercraft can be spawned and the counter resets
        cooldown: int = 1;
        //------------
        // constraints:
        C_counter: $ self.counter >= 0 $ /// Counter needs to be positive
        C_rate: $ self.rate > 0 $ /// Rate needs to be strictly positive
        C_cooldown_lower_bound: $ self.cooldown >= 1 $ /// Cooldown need to be positive
        C_cooldown_upper_bound: $ self.cooldown <= self.rate $ /// Cooldown can not exceed rate
    }

    Node Segment[0..*]: Shippable, InNode, OutNode {
        //------------
        // constraints:
        C_in_is_not_out: $ self.in0 <> self.out0 $ /// The node that is references by the in and out should not be the same
    }

    Node Confluence[0..*]: Shippable, InNode, OutNode {
        /// Indicates if it is the turn to check the first direction
        last_direction: boolean = false; // Is false if the last watercraft came from in0 else it is true
        in1: OutNode;
        out1: InNode;
        //------------
        // constraints:
        // Pairwise non-equality between in/out
        C_in1_is_not_out1: $ self.in1 <> self.out1 $
        C_in0_is_not_out1: $ self.in0 <> self.out1 $
        C_in1_is_not_out0: $ self.in1 <> self.out0 $
        C_in0_is_not_in1:  $ self.in0 <> self.in1 $
        C_out0_is_not_out1: $ self.out0 <> self.out1 $
        /// No in/out to itself
        C_in1_not_self: $ self.in1 <> self $
        C_out1_not_self: $ self.out1 <> self $
        /// Similar to the checks is InNode and OutNode just for in1 and out 1
        C_outNode_connection_match: $ (self.in1.out0 == self) xor (self.in1.isKindOf(Confluence) and self.in1.out1 == self) $
        C_inNode_connection_match: $ (self.out1.in0 == self) xor (self.out1.isKindOf(Confluence) and self.out1.in1 == self) $
    }

    //================================================
    // Model-level constraints:
    // Need check for the edges
    
    /// Check that no watercrafts are lost along the way
    C_no_watercraft_loss: $ Source.all().collect(s | s.counter).sum() == (Sink.all().collect(s | s.counter).sum() + Shippable.all().select(s | s.has_watercraft).size()) $
    /// #Source needs to match the #Sink
    C_equal_sinks_and_sources: $ Source.all().size() == Sink.all().size() $

    Node Schedule[1]{
        list: AnyNode[*];
        //------------
        // constraints:
        /// All nodes need to be in the schedule, that is the amount of unique entries needs to be
        /// equal to the amount of AnyNodes in the Model
        C_all_states_in_schedule: $ self.list.asSet().size() == AnyNode.all().size() $
        C_no_duplicates: $ self.list.size() == self.list.asSet().size() $
    }
   
}

// crossroads
WMS WMS_TEST {
    Source source0 {
        rate = 1;
        out0 = segment0;
    }

    Source source1 {
        rate = 1;
        out0 = segment1;
    }

    Segment segment0 {
        in0 = source0;
        out0 = confluence;
    }

    Segment segment1 {
        in0 = source1;
        out0 = confluence;
    }

    Segment segment2 {
        in0 = confluence;
        out0 = sink0;
    }

    Segment segment3 {
        in0 = confluence;
        out0 = sink1;
    }

    Confluence confluence {
        in0 = segment0;
        in1 = segment1;
        out0 = segment2;
        out1 = segment3;
    }

    Sink sink0 {
        in0 = segment2;
    }

    Sink sink1 {
        in0 = segment3;
    }
    
    Schedule schedule {
        list = [sink0, segment2, sink1, segment3, confluence, segment1, segment0, source0, source1];
    }
}
